plugins {
    id 'org.springframework.boot' version '3.1.3' apply false
    id 'io.spring.dependency-management' version '1.1.6' apply false
    id 'checkstyle'
    id "org.sonarqube" version "3.5.0.2730"
}

allprojects {
    group = 'com.example'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    apply plugin: "org.sonarqube"
}

sonarqube {
    properties {
        property "sonar.projectKey", "com.example:account"
        property "sonar.projectName", "Account Service"
        property "sonar.host.url", "http://localhost:9000"
        property "sonar.login", "your-sonarqube-token"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation 'org.springframework.boot:spring-boot-starter-logging'
        implementation 'org.springframework.boot:spring-boot-starter-aop'
        implementation 'org.springframework.boot:spring-boot-starter-data-redis'

        implementation 'org.redisson:redisson:3.17.1'
        implementation('it.ozimov:embedded-redis:0.7.3') {
            exclude group: "org.slf4j", module: "slf4j-simple"
        }

        implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
        runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
        runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

        implementation 'javax.xml.bind:jaxb-api:2.3.1'
        compileOnly 'org.projectlombok:lombok'
        runtimeOnly 'com.h2database:h2'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    }

    tasks.named('test') {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    checkstyle {
        maxWarnings = 0
        configFile = file("${rootDir}/config/naver-checkstyle-rules.xml")
        configProperties = ["suppressionFile": "${rootDir}/config/naver-checkstyle-suppressions.xml"]
        toolVersion = "8.42"
    }

    jacoco {
        toolVersion = "0.8.7"
    }

    jacocoTestReport {
        reports {
            xml.required = true
        }
    }
}
